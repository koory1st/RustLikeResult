plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.koory1st'
version '1.0.14'

[compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:22.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifactId 'rust-like-result'
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'rust-like-result'
                description = "Rust like result"
                url = 'https://github.com/koory1st/RustLikeResult'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/koory1st/RustLikeResult/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'koory1st'
                        name = 'koory1st'
                        email = 'liguenator@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:koory1st/RustLikeResult.git"
                    developerConnection = "scm:git:git@github.com:koory1st/RustLikeResult.git"
                    url = "https://github.com/koory1st/RustLikeResult"
                }
            }
        }
    }
}
signing {
    sign publishing.publications.mavenJava
}